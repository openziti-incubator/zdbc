/*
 * Copyright (c) 2018-2021 NetFoundry, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java'
    id 'io.wusa.semver-git-plugin' version "2.3.7"
    id 'io.github.gradle-nexus.publish-plugin' version "1.1.0"
    id 'maven-publish'
}

repositories {
    mavenCentral()
}


sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

ext {
    zdbcBuildnum = System.getenv("BUILD_NUMBER") ?: "local"
}

configurations {
   testCompile
}
   
group = "org.openziti"


semver {
    initialVersion = "0.1.0"
    tagType = io.wusa.TagType.LIGHTWEIGHT
    branches {
        branch {
            regex = "main"
            incrementer = "PATCH_INCREMENTER"
            formatter = { info -> "${info.version.major}.${info.version.minor}.${info.version.patch}" }
        }
        branch {
            regex = ".+"
            incrementer = "PATCH_INCREMENTER"
            formatter = { info ->
                def v = info.version
                """${v.major}.${v.minor}.${v.patch}-${v.suffix?.count ?: "0"}.${v.suffix?.sha}-${info.branch.id}"""
            }
        }
    }
}


ext {
    gitCommit = semver.info.shortCommit
    gitBranch = semver.info.branch.name
}

version = "${semver.info}"

dependencies {
  	compileOnly 'org.openziti:ziti:0.22.+'
}

tasks.withType(PublishToMavenRepository).all {
    onlyIf { !semver.info.dirty }
}

publishing {
    publications {
        zdbcJava(MavenPublication) {
            from components.java
        }
    }
}

apply from: rootProject.file('publish.gradle')
